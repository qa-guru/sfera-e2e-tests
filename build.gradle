plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.9.6"
    id "io.freefair.lombok" version "6.0.0-m2"
    id "org.gradle.test-retry" version "1.3.1"
    id "com.google.protobuf" version "0.8.17"
}

repositories {
    mavenCentral()
}

group 'zone.sfera.tests'
version '1.0'

def allureVersion = "2.17.3",
    selenideVersion = "6.6.6",
    appiumVersion = "8.1.1",
    junitVersion = "5.8.2",
    slf4jVersion = "1.7.32",
    gsonVersion = "2.9.0",
    protobufVersion = "3.15.5",
    grpcVersion = "1.36.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    implementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "io.grpc:grpc-all:$grpcVersion",
            "com.google.protobuf:protobuf-java:$protobufVersion",
            "com.google.protobuf:protobuf-java-util:$protobufVersion",
            'javax.annotation:javax.annotation-api:1.3.2'
    )

    testImplementation(
            "com.github.javafaker:javafaker:1.0.2",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:$appiumVersion",
            "io.rest-assured:rest-assured:4.5.1",
            "commons-io:commons-io:2.11.0",
            "org.apache.commons:commons-collections4:4.4",
            "org.aeonbits.owner:owner:1.0.12",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "com.google.code.gson:gson:$gsonVersion"
    )
    protobuf files("${projectDir}/src/test/resources/proto")
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()
    retry {
        maxRetries = 1
        maxFailures = 20
        failOnPassedAfterRetry = false
    }

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task android(type: Test) {
    useJUnitPlatform { includeTags 'android' }
}

task ios(type: Test) {
    useJUnitPlatform { includeTags 'ios' }
}

task login(type: Test) {
    useJUnitPlatform { includeTags 'login' }
}

task profile(type: Test) {
    useJUnitPlatform { includeTags 'profile' }
}

task settings(type: Test) {
    useJUnitPlatform { includeTags 'settings' }
}

task grpc(type: Test) {
    useJUnitPlatform { includeTags 'grpc' }
}
